plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.pluginlangcore'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    // Paper API - compileOnly because servers provide this
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'

    // Lombok for cleaner code
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

javadoc {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github'
            artifactId = 'pluginlangcore'
            version = project.version
            from components.java

            pom {
                name = 'PluginLangCore'
                description = 'A comprehensive language and message management library for Minecraft plugins'
                url = 'https://github.com/NighterDevelopment/PluginLangCore'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'nighter'
                        name = 'Nighter'
                        email = 'notnighter@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/NighterDevelopment/PluginLangCore.git'
                    developerConnection = 'scm:git:ssh://github.com/NighterDevelopment/PluginLangCore.git'
                    url = 'https://github.com/NighterDevelopment/PluginLangCore'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/NighterDevelopment/PluginLangCore")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}